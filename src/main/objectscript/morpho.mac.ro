Cache for UNIX^MAC^WNS^^~Format=Cache.S~^UTF8
%RO on 21 Май 2014 04:13:05PM
morpho^MAC^^60821,57389^0
morpho ; Примеры работы с %Text.Russian (загрузка словаря, загрузка документов)
#include ft
#include %occStatus
 /* примеры запросов
 
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINSTERM ('хламидиоз','инфекция')
 
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINSTERM ('крупозная','пневмония')
 
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINSTERM ('челюстно','лицевого','Франческетти','Цвалена') 
или:
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINS ('челюстно-лицевого','Франческетти','Цвалена')
 
 */
	
ftload(init,folder) ; init#2 - перезагружает словари ; init\2#2 - перезагружает документы. Т.е.:
			 ; 0 - ничего не делает со словарями, добавит в БД документы;
			 ; 1 - перезагрузит словари и добавит в БД документы, 
			 ; 2 - сотрет старое содержимое БД документов и добавит в БД документы
			 ; 3 - перезагрузит словари, сотрет старое содержимое БД документов и добавит в БД документы
			 ;
			 ; folder - каталог, где хранятся файл словаря morphs.mrd и тексты для загрузки в ft.doc
			 ;
			 ; NB! ^CacheTempMorpho стирается перед прогоном всегда! (он заполняется, если #%Text.Russian.DEBUG=1)
	i $g(folder)="" w !,"Каталог для загрузки словаря и/или текстов не задан" q
	s:$e(folder,$l(folder))'="\" folder=folder_"\"
	k ^CacheTempMorpho ;($j)
	if $g(init)#2 {
		s rc=##class(%Text.Russian).DropDictionary()
		i rc s rc=##class(%Text.Russian).LoadDict(folder_"morphs.mrd") ;именно в таком порядке! ;AM01
		i rc s rc=##class(%Text.Russian).ExcludeCommonTerms(1000)
		k ^DictNew
		i 'rc g fterrex
		w !,"Словари загружены"
	}
	if $g(init)\2#2 {
		s rc=##class(ft.doc).%KillExtent() i 'rc g fterrex
		w !,"Хранилище документов очищено"
	}
	d $zu(68,40,1)
	s t0=$zh,bc=0,mask=folder_"*.txt"
	f  s file=$zsearch(mask) s mask="" q:file=""  s rc=$$ftpar(file) i 'rc u $p w !,rc q
	s dt=$zh-t0 u $p w !,$g(bc)/1024," kChars; ",dt," sec; " i $g(bc)>0 w $j(bc/dt/1024,10,2)," kChars/sec"
fterrex
	i 'rc { d $system.OBJ.DisplayError(rc) } 
	q 
 
 
ftpar(file) ; read per paragraph (myMAXLEN max size) ; global bc
	n line1
	c file o file:("RK\CP1251\"):0 e  q "0~not opened"
	u $p w !,file ;r *a
	u file
	u file s tag=0, maxlen=0
	k line1
	s text="",npar=1,eop=1
	s doc=##class(ft.doc).%New(),doc.myTitle=file,doc.myPar=npar
	for {
		s line=$$ftpline(.eop)
		while line'="" {
			if $l(text)+$l(line)<=$$$myMAXLEN  { s text=text_" "_line, line="" }
			else { s text=text_$e(line,1,$$$myMAXLEN-$l(text)), line=$e(line,$$$myMAXLEN-$l(text)+1,$l(line)), eop=1 }
			if eop || (line'="") {
				s doc.myDocument=text s rc=doc.%Save()
				#; u $p w !,text r *a u file ;de!
				s npar=npar+1,bc=bc+$l(text),text="" q:'rc
				s doc=##class(ft.doc).%New(),doc.myTitle=file,doc.myPar=npar
			}
		}
		s rc=1
		q:$zeof
	}
	s doc=""
	c file
	q rc
 
ftpline(eop)
	s eop=1,line=""
	if $g(line1)="" {
	  do {
		r line1
		g:$zeof ftplex
	  } while line1=""
	}
	s line=line1 ;s maxlen=$s($l(line)>maxlen:$l(line),1:maxlen)
	r line1	g:$zeof ftplex
	if line1="" { s eop=1 }
	elseif $e(line1,1,4)=$c(32,32,32,32) { s eop=1 }
	else { s eop=0 }
	if eop {
		s endch=$e(line,*)
		i endch="""" s endch=$e(line,*-1) i endch=" " s line=$zu(28,$e(line,1,*-1),8),$e(line,1)="",endch=$e(line,*),line=line_""""
		i endch=" " s line=$zu(28,line,8),$e(line,1)="",endch=$e(line,*)
		s eop=$s(endch=".":1,endch="?":1,endch="!":1,endch=":":1,1:0)
	}
	s:'eop&&(endch'="-") line=line_" "
ftplex
	q line
 
 



