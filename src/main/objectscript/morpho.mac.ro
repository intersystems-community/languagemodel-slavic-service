Cache for UNIX^MAC^WNS^^~Format=Cache.S~^UTF8
%RO on 21 Май 2014 04:31:59PM
morpho^MAC^^63328,59436.706821^0
morpho ; Примеры работы с %Text.Russian (загрузка словаря, загрузка документов)
#include ft
#include %occStatus
 /* примеры запросов
 
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINSTERM ('хламидиоз','инфекция')
 
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINSTERM ('крупозная','пневмония')
 
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINSTERM ('челюстно','лицевого','Франческетти','Цвалена') 
или:
SELECT ID , myDocument, myTitle FROM ft.doc where myDocument %CONTAINS ('челюстно-лицевого','Франческетти','Цвалена')
 
 */
	
ftload(init,folder) ; init#2 - перезагружает словари ; init\2#2 - перезагружает документы. Т.е.:
			 ; 0 - ничего не делает со словарями, добавит в БД документы;
			 ; 1 - перезагрузит словари и добавит в БД документы, 
			 ; 2 - сотрет старое содержимое БД документов и добавит в БД документы
			 ; 3 - перезагрузит словари, сотрет старое содержимое БД документов и добавит в БД документы
			 ;
			 ; folder - каталог, где хранятся файл словаря morphs.mrd и тексты для загрузки в ft.doc
			 ;
			 ; NB! ^CacheTempMorpho стирается перед прогоном всегда! (он заполняется, если #%Text.Russian.DEBUG=1)
	if $get(folder)="" write !,"Каталог для загрузки словаря и/или текстов не задан" quit
	set:$extract(folder,$length(folder))'="\" folder=folder_"\"
	kill ^CacheTempMorpho ;($j)
	if $get(init)#2 {
		set rc=##class(%Text.Russian).DropDictionary()
		if rc set rc=##class(%Text.Russian).LoadDict(folder_"morphs.mrd") ;именно в таком порядке! ;AM01
		if rc set rc=##class(%Text.Russian).ExcludeCommonTerms(1000)
		kill ^DictNew
		if 'rc goto fterrex
		write !,"Словари загружены"
	}
	if $get(init)\2#2 {
		set rc=##class(ft.doc).%KillExtent() if 'rc goto fterrex
		write !,"Хранилище документов очищено"
	}
	do $zutil(68,40,1)
	set t0=$zhorolog,bc=0,mask=folder_"*.txt"
	for  set file=$zsearch(mask) set mask="" quit:file=""  set rc=$$ftpar(file) if 'rc use $principal write !,rc quit
	set dt=$zhorolog-t0 use $principal write !,$get(bc)/1024," kChars; ",dt," sec; " if $get(bc)>0 write $justify(bc/dt/1024,10,2)," kChars/sec"
fterrex
	if 'rc { do $system.OBJ.DisplayError(rc) } 
	quit 
 
 
ftpar(file) ; read per paragraph (myMAXLEN max size) ; global bc
	new line1
	close file open file:("RK\CP1251\"):0 else  quit "0~not opened"
	use $principal write !,file ;r *a
	use file
	use file set tag=0, maxlen=0
	kill line1
	set text="",npar=1,eop=1
	set doc=##class(ft.doc).%New(),doc.myTitle=file,doc.myPar=npar
	for {
		set line=$$ftpline(.eop)
		while line'="" {
			if $length(text)+$length(line)<=$$$myMAXLEN  { set text=text_" "_line, line="" }
			else { set text=text_$extract(line,1,$$$myMAXLEN-$length(text)), line=$extract(line,$$$myMAXLEN-$length(text)+1,$length(line)), eop=1 }
			if eop || (line'="") {
				set doc.myDocument=text set rc=doc.%Save()
				#; u $p w !,text r *a u file ;de!
				set npar=npar+1,bc=bc+$length(text),text="" quit:'rc
				set doc=##class(ft.doc).%New(),doc.myTitle=file,doc.myPar=npar
			}
		}
		set rc=1
		quit:$zeof
	}
	set doc=""
	close file
	quit rc
 
ftpline(eop)
	set eop=1,line=""
	if $get(line1)="" {
	  do {
		read line1
		goto:$zeof ftplex
	  } while line1=""
	}
	set line=line1 ;s maxlen=$s($l(line)>maxlen:$l(line),1:maxlen)
	read line1	goto:$zeof ftplex
	if line1="" { set eop=1 }
	elseif $extract(line1,1,4)=$char(32,32,32,32) { set eop=1 }
	else { set eop=0 }
	if eop {
		set endch=$extract(line,*)
		if endch="""" set endch=$extract(line,*-1) if endch=" " set line=$zutil(28,$extract(line,1,*-1),8),$extract(line,1)="",endch=$extract(line,*),line=line_""""
		if endch=" " set line=$zutil(28,line,8),$extract(line,1)="",endch=$extract(line,*)
		set eop=$select(endch=".":1,endch="?":1,endch="!":1,endch=":":1,1:0)
	}
	set:'eop&&(endch'="-") line=line_" "
ftplex
	quit line
 
 



